
plugins {
    id 'org.springframework.boot' version '3.0.0'
    id 'jacoco' // core plugin provided by gradle
    id 'org.flywaydb.flyway' version '9.8.3'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}


apply plugin: 'java' // core plugin provided by gradle

group = 'com.seebie'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}


// use preview features
tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
    // options.compilerArgs += "-Xlint:preview"
    options.compilerArgs += "-Xlint:unchecked"
    options.compilerArgs += "-Xlint:deprecation"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

dependencies {

    implementation 'org.postgresql:postgresql:42.6.0'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.1.4'
    implementation 'org.springframework.session:spring-session-jdbc:3.1.2'

    implementation 'org.apache.commons:commons-csv:1.10.0'

    compileOnly 'org.hibernate:hibernate-jpamodelgen:6.3.1.Final'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen:6.3.1.Final'

    implementation 'org.flywaydb:flyway-core:9.22.2'

    testImplementation 'org.springframework.security:spring-security-test:6.1.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.4'
    testImplementation 'net.datafaker:datafaker:2.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'

    testImplementation "org.testcontainers:testcontainers:1.19.0"
    testImplementation "org.testcontainers:junit-jupiter:1.19.0"
    testImplementation "org.testcontainers:postgresql:1.19.0"
}


sourceSets {
    generated {
        java {
            srcDirs = ['build/generated/sources']
        }
    }
}

// Since Spring Boot 2.5, it builds both an executable jar and a plain jar suitable for use as a library.
// This block prevents building the "plain" jar so we only build the executable jar
jar {
    enabled = false
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {

    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = file("${buildDir}/reports/jacoco/html")
    }

    // so we can get test data from both tests and integration tests
    getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/**/jacocoTest.exec"))

    // ignore code that's not a good fit for unit testing
    // e.g. getters and setters in JPA entities
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/entity/*"
            ])
        }))
    }
}

// integration test by tags,
// inspired by https://stackoverflow.com/questions/40891867/junit5-tag-specific-gradle-task

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/test/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/test/classpathdumps")
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

def integrationTest = tasks.register("integrationTest", Test) {

    // this lets the container stay up so you can run standalone later with the same container if you want
    // the flag here merely allows you to use the setting, to actually leave a container up (or not), call
    // the container's .withReuse() method
    environment "TESTCONTAINERS_REUSE_ENABLE", "true"

    // It takes a couple of seconds, but if you want to speed up your tests,
    // you can disable the checks once you have everything configured
    // TODO I think this overwrites the above? Maybe you can only make one environment call here
    // environment "TESTCONTAINERS_CHECKS_DISABLE", "true"

    useJUnitPlatform {
        includeTags "integration"
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/integrationTest/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/integrationTest/classpathdumps")
    }

    finalizedBy jacocoTestReport // report is always generated after tests run
}

tasks.named("check") {
    dependsOn integrationTest
}

task copyWebApp {
    description 'Copies built files from reactjs to server'
    dependsOn ':reactjs:build'
    doLast {
        copy {
            from(project(":reactjs").buildDir)
            into(project(":server").buildDir.toPath().resolve('resources/main/static'))
        }
    }
}

bootJar.dependsOn('copyWebApp')


task genKey(type:Exec) {

    var p12File = 'app.p12'

    if( file(p12File).exists()) {
        commandLine 'keytool', '-list', '-storepass', 'password'
    }
    else {
        commandLine 'keytool', '-genkeypair', '-alias', 'app', '-keyalg', 'RSA', '-keysize', '2048', '-validity', '3650',
                '-storetype', 'PKCS12', '-keystore', p12File, '-storepass', 'password', '-keypass', 'password',
                '-dname', 'cn=Unknown, ou=Unknown, o=Unknown, c=Unknown'
    }

}

genKey.dependsOn('compileJava')
bootJar.dependsOn('genKey')
bootRun.dependsOn('genKey')
